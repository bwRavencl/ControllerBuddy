buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:2.1.0'
    }
}

plugins {
    id 'com.stehno.natives' version '0.3.1'
    id 'us.kirchmeier.capsule' version '1.0.2'
}

apply plugin: 'application'

def genDir = 'gen'
def versionDir = "$genDir/main/java"

task generateVersion {
    def outputDir = file("$versionDir")
    outputs.dir outputDir
    doFirst {
        def srcFile = new File(outputDir, "de/bwravencl/controllerbuddy/Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write("""
package de.bwravencl.controllerbuddy;
public class Version {
   public static String getVersion() { return "$project.version"; }
}
""")
    }
}

task cleanVersion {
    delete genDir
}

clean {
    dependsOn cleanVersion
}

compileJava {
    dependsOn generateVersion
    source generateVersion.outputs.files, sourceSets.main.java
}

ext {
    git = org.ajoberstar.grgit.Grgit.open()
}

mainClassName = "de.bwravencl.controllerbuddy.gui.Main"
version = "0.5-${git.head().abbreviatedId}"

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compile 'commons-cli:commons-cli:1.4',
            'com.google.code.gson:gson:2.8.2',
            'net.java.dev.jna:jna:4.5.1',
            'net.java.dev.jna:jna-platform:4.5.1',
            'net.java.jinput:jinput:2.0.7'
    compile fileTree(dir: 'lib', include: ['*.jar'])
}

jar {
    exclude '**/de/bwravencl/controllerbuddy/extras'
}

natives {
    libraries {
        include = ['jinput-dx8.dll', 'jinput-dx8_64.dll', 'jinput-raw.dll', 'jinput-raw_64.dll', 'jinput-wintab.dll', 'libjinput-linux.so', 'libjinput-linux64.so', 'libjinput-osx.jnilib']
    }
}

run {
    dependsOn includeNatives
    systemProperty 'java.library.path', "$buildDir/natives"
}

task capsule(type: FatCapsule){
    dependsOn includeNatives
    applicationClass 'de.bwravencl.controllerbuddy.gui.Main'
    baseName 'ControllerBuddy'
    from fileTree(dir: "$buildDir/natives", includes: ['*.dll', '*.jnilib', '*.so'])
}

apply plugin: 'eclipse'

tasks.eclipse.dependsOn generateVersion, includeNatives

eclipse {
    classpath {
        file {
            withXml {
                def node = it.asNode()
                node.find { it.@path.endsWith('jinput-platform-2.0.7-natives-windows.jar') }.find { child-> child.name() == 'attributes' }.appendNode('attribute', [name: 'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:"$buildDir/natives"])
                node.find { it.@path.endsWith('jinput-platform-2.0.7-natives-osx.jar') }.find { child-> child.name() == 'attributes' }.appendNode('attribute', [name: 'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:"$buildDir/natives"])
                node.find { it.@path.endsWith('jinput-platform-2.0.7-natives-linux.jar') }.find { child-> child.name() == 'attributes' }.appendNode('attribute', [name: 'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:"$buildDir/natives"])
            }
            whenMerged { cp ->
                cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder("$versionDir", null) )
            }
        }
    }
}
